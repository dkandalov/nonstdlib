buildscript {
	ext.kotlin_version = "1.3.0"
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

group "dkandalov"
version "0.1.5"

apply plugin:"kotlin"

repositories {
	mavenCentral()
    ivy {
        layout "pattern"
        artifactPattern "http://raw.githubusercontent.com/dkandalov/kotlin-common-test/master/jars/[organisation]-[artifact]-[revision](-[classifier])(.[ext])"
    }
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
	testCompile "junit:junit:4.12"
	testCompile "dkandalov:kotlin-common-test:0.1.2"
}

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

task sourcesJar(type:Jar, dependsOn:classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

task publishJars(dependsOn:build) {
	doLast {
		ant.copy(
			file:"build/libs/kotlin-common-${version}.jar",
			toFile:"jars/dkandalov-kotlin-common-${version}.jar"
		)
		ant.copy(
			file:"build/libs/kotlin-common-${version}-sources.jar",
			toFile:"jars/dkandalov-kotlin-common-${version}-sources.jar"
		)
}}